# 0=${(%):-%N}
export ZSH="${ZSH:-${0:A:h}}"
export ZSH_CUSTOMDIR="${ZSH_CUSTOM:-$HOME/.zsh}"

ZSH_LIBDIR="${ZSH}/lib"
ZSH_PLUGINSDIR="${ZSH}/plugins"

ZSH_HISTFILE="${ZSH_HISTFILE:-${HOME}/.zsh_history}"
ZSH_HISTSIZE=${ZSH_HISTSIZE:-30000}

[ -z "$ZSH_PLUGINS" ] && ZSH_PLUGINS=( )

autoload -U compaudit compinit bashcompinit
autoload -U promptinit && promptinit
autoload -U colors && colors

# a command for renaming files by means of shell patterns
autoload -U zmv
# zargs, as an alternaive to find -exec and xargs
autoload -U zargs
autoload -U add-zsh-hook

autoload -U run-help
autoload run-help-git

# history
HISTFILE="${ZSH_HISTFILE}"
HISTSIZE="${ZSH_HISTSIZE}"
SAVEHIST="${HISTSIZE}"

setopt append_history
setopt inc_append_history
setopt hist_expire_dups_first
setopt hist_ignore_space
setopt hist_ignore_all_dups

# directory
setopt auto_name_dirs
setopt auto_pushd
setopt pushd_silent
setopt pushd_to_home
# remove duplicate entries
setopt pushd_ignore_dups
# reverts the +/- operators
setopt pushd_minus

setopt extended_glob

# ignore lines prefixed with '#'
setopt interactive_comments

for config ($ZSH_LIBDIR/*.zsh); do
    source $config
done

for plugin ($ZSH_PLUGINS); do
    if [ -d "${ZSH_CUSTOMDIR}/${plugin}" ] ; then
        fpath=( "${ZSH_CUSTOMDIR}/${plugin}" $fpath )
    elif [ -d "${ZSH_PLUGINSDIR}/${plugin}" ] ; then
        fpath=( "${ZSH_PLUGINSDIR}/${plugin}" $fpath )
    fi
done

for plugin (${ZSH_PLUGINS}); do
    if [ -f "${ZSH_CUSTOMDIR}/${plugin}/${plugin}.plugin.zsh" ] ; then
        source "${ZSH_CUSTOMDIR}/${plugin}/${plugin}.plugin.zsh"
    elif [ -f "${ZSH_PLUGINSDIR}/${plugin}/${plugin}.plugin.zsh" ] ; then
        source "${ZSH_PLUGINSDIR}/${plugin}/${plugin}.plugin.zsh"
    else
        echo "[ZSH] Plugin '${plugin}' not found"
    fi
done

compinit
bashcompinit
